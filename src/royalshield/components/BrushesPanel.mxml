<?xml version="1.0" encoding="utf-8"?>

<s:BorderContainer xmlns:fx="http://ns.adobe.com/mxml/2009"
                   xmlns:s="library://ns.adobe.com/flex/spark"
                   xmlns:mx="library://ns.adobe.com/flex/mx"
                   xmlns:ide="royalshield.components.*"
                   backgroundColor="0x494949"
                   borderColor="0x272727"
                   creationComplete="creationCompleteHandler(event)">
    
    <fx:Metadata>
        [Event(name="brushChange", type="royalshield.events.BrushEvent")]
    </fx:Metadata>
    
    <fx:Script>
        <![CDATA[
            import mx.core.FlexGlobals;
            import mx.events.FlexEvent;
            
            import royalshield.brushes.BrushType;
            import royalshield.components.BrushButton;
            import royalshield.core.IRoyalShieldIDE;
            import royalshield.events.BrushEvent;
            import royalshield.utils.isNullOrEmpty;
            
            //--------------------------------------------------------------------------
            // PROPERTIES
            //--------------------------------------------------------------------------
            
            private var m_application:IRoyalShieldIDE;
            private var m_selectedType:String;
            
            //--------------------------------------
            // Getters / Setters 
            //--------------------------------------
            
            public function get selectedType():String { return m_selectedType; }
            public function set selectedType(value:String):void
            {
                if (m_selectedType != value) {
                    if (selectButton(value)) {
                        m_selectedType = value;
                        dispatchEvent(new BrushEvent(BrushEvent.BRUSH_CHANGE, m_selectedType));
                    }
                }
            }
            
            //--------------------------------------------------------------------------
            // METHODS
            //--------------------------------------------------------------------------
            
            //--------------------------------------
            // Private
            //--------------------------------------
            
            private function selectButton(type:String):Boolean
            {
                if (isNullOrEmpty(type))
                    return false;
                
                var hasTool:Boolean;
                for (var i:int = 0; i < numElements; i++) {
                    var button:BrushButton = BrushButton(getElementAt(i));
                    if (!button) continue;
                    
                    button.selected = false;
                    if (button.brushType == type) {
                        button.selected = true;
                        hasTool = true;
                    }
                }
                return hasTool;
            }
            
            //--------------------------------------
            // Event Handlers
            //--------------------------------------
            
            protected function creationCompleteHandler(event:FlexEvent):void
            {
                m_application = IRoyalShieldIDE(FlexGlobals.topLevelApplication);
                m_application.removeEventListener(FlexEvent.CREATION_COMPLETE, creationCompleteHandler);
                m_application.systemManager.stage.addEventListener(KeyboardEvent.KEY_DOWN, keyDownHandler);
            }
            
            protected function buttonClickHandler(event:Event):void
            {
                var type:String = BrushButton(event.target).brushType;
                if (m_selectedType == type)
                    BrushButton(event.target).selected = true;
                else 
                    selectedType = type;
            }
            
            override protected function keyDownHandler(event:KeyboardEvent):void
            {
                var keyCode:uint = event.keyCode;
                if (!event.ctrlKey && !event.shiftKey) {
                    switch(keyCode)
                    {
                        case Keyboard.B:
                            this.selectedType = BrushType.BRUSH;
                            break;
                        
                        case Keyboard.E:
                            this.selectedType = BrushType.ERASER;
                            break;
                    }
                }
            }
            
        ]]>
    </fx:Script>
    
    <s:layout>
        <s:VerticalLayout paddingLeft="3"
                          paddingRight="3"
                          paddingTop="5"
                          paddingBottom="5"
                          gap="5"
                          horizontalAlign="center"/>
    </s:layout>
    
    <ide:BrushButton id="brushButton"
                     width="30"
                     height="30"
                     label="B"
                     toolTip="Brush (B)"
                     brushType="{BrushType.BRUSH}"
                     icon="@Embed('../assets/brush_icon.png')"
                     click="buttonClickHandler(event)"/>
    
    <ide:BrushButton id="eraseButton"
                     width="30"
                     height="30"
                     label="E"
                     toolTip="Erase (E)"
                     brushType="{BrushType.ERASER}"
                     icon="@Embed('../assets/eraser_icon.png')"
                     click="buttonClickHandler(event)"/>
</s:BorderContainer>

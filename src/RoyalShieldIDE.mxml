<?xml version="1.0" encoding="utf-8"?>

<s:WindowedApplication xmlns:fx="http://ns.adobe.com/mxml/2009"
                       xmlns:s="library://ns.adobe.com/flex/spark"
                       xmlns:mx="library://ns.adobe.com/flex/mx"
                       xmlns:ide="royalshield.components.*"
                       xmlns:menu="royalshield.components.menu.*"
                       implements="royalshield.core.IRoyalShieldIDE"
                       minWidth="800"
                       minHeight="600"
                       showStatusBar="false"
                       backgroundColor="0x494949"
                       preinitialize="applicationPreinitializeHandler(event)"
                       creationComplete="applicationCreationCompleteHandler(event)" >
    
    <fx:Style source="royal_shield.css"/>
    
    <fx:Script>
        <![CDATA[
            import mx.events.FlexEvent;
            
            import royalshield.brushes.BrushManager;
            import royalshield.brushes.BrushType;
            import royalshield.brushes.IBrushManager;
            import royalshield.drawing.DrawingManager;
            import royalshield.drawing.IDrawingManager;
            import royalshield.events.BrushEvent;
            import royalshield.events.MenuEvent;
            import royalshield.history.HistoryActionGroup;
            import royalshield.history.IHistoryManager;
            import royalshield.history.RSHistoryManager;
            import royalshield.settings.ISettingsManager;
            import royalshield.settings.RoyalShieldSettings;
            import royalshield.settings.SettingsManager;
            import royalshield.utils.DescriptorUtil;
            import royalshield.world.IWorldMap;
            import royalshield.world.WorldMap;
            
            //--------------------------------------------------------------------------
            // PROPERTIES
            //--------------------------------------------------------------------------
            
            private var m_settingsManager:ISettingsManager;
            private var m_settings:RoyalShieldSettings;
            private var m_brushManager:IBrushManager;
            private var m_drawingManager:IDrawingManager;
            private var m_historyManager:IHistoryManager;
            private var m_worldMap:IWorldMap;
            
            //--------------------------------------
            // Getters / Setters 
            //--------------------------------------
            
            public function get settingsManager():ISettingsManager { return m_settingsManager; }
            
            //--------------------------------------------------------------------------
            // METHODS
            //--------------------------------------------------------------------------
            
            //--------------------------------------
            // Private
            //--------------------------------------
            
            private function loadSettings():void
            {
                m_settings = new RoyalShieldSettings();
                m_settingsManager.loadSettings(m_settings);
            }
            
            private function test():void
            {
                m_worldMap = new WorldMap();
                editorPanel.worldMap = m_worldMap;
                editorPanel.mapDisplay.onMouseMapChanged.add(mouseMapChangedCallback);
                m_drawingManager.addTarget(editorPanel.mapDisplay);
                RSHistoryManager(m_historyManager).map = m_worldMap;
            }
            
            //--------------------------------------
            // Event Handlers
            //--------------------------------------
            
            protected function applicationPreinitializeHandler(event:FlexEvent):void
            {
                m_settingsManager = SettingsManager.getInstance();
                m_brushManager = BrushManager.getInstance();
                m_brushManager.cursorManager = this.cursorManager;
                m_drawingManager = DrawingManager.getInstance();
                m_historyManager = RSHistoryManager.getInstance();
                m_historyManager.addActionGroup(new HistoryActionGroup("default"));
                
                loadSettings();
            }
            
            protected function applicationCreationCompleteHandler(event:FlexEvent):void
            {
               this.title = DescriptorUtil.getName() + " " + DescriptorUtil.getVersionLabel();
               this.systemManager.stage.addEventListener(KeyboardEvent.KEY_DOWN, keyDownHandler);
               
               test();
            }
            
            protected function nativeMenuSelectedHandler(event:MenuEvent):void
            {
                switch(event.data)
                {
                    case IDEMenu.HELP_ABOUT:
                        break;
                }
            }
            
            protected function toolsPanelBrushChangeHandler(event:BrushEvent):void
            {
                m_brushManager.brushType = event.brushType;
            }
            
            override protected function keyDownHandler(event:KeyboardEvent):void
            {
                var keyCode:uint = event.keyCode;
                if (!event.controlKey && !event.shiftKey) {
                    switch(keyCode)
                    {
                        case Keyboard.B:
                            toolsPanel.selectedType = BrushType.BRUSH;
                            break;
                        
                        case Keyboard.E:
                            toolsPanel.selectedType = BrushType.ERASER;
                            break;
                        
                        case Keyboard.UP:
                            editorPanel.onMoveMapNorth();
                            break;
                        
                        case Keyboard.DOWN:
                            editorPanel.onMoveMapSouth();
                            break;
                        
                        case Keyboard.LEFT:
                            editorPanel.onMoveMapWest();
                            break;
                        
                        case Keyboard.RIGHT:
                            editorPanel.onMoveMapEast();
                            break;
                    }
                } else if (event.controlKey && !event.shiftKey) {
                    switch(keyCode)
                    {
                        case Keyboard.Z:
                            m_historyManager.undo();
                            break;
                        
                        case Keyboard.Y:
                            m_historyManager.redo();
                            break;
                    }
                }
            }
            
            private function mouseMapChangedCallback(x:uint, y:uint, z:uint):void
            {
                if (statusbar && statusbar.positionLabel)
                    statusbar.positionLabel.text = "x:" + x + ", y:" + y + ", z:" + z;
            }
            
        ]]>
    </fx:Script>
    
    <s:menu>
        <menu:IDEMenu id="nativeMenu"
                      selected="nativeMenuSelectedHandler(event)"/>
    </s:menu>
    
    <ide:Toolbar width="100%"
                 height="35"/>
    
    <ide:ToolsPanel id="toolsPanel"
                    width="50"
                    height="100%"
                    top="35"
                    bottom="23"
                    brushChange="toolsPanelBrushChangeHandler(event)"/>
    
    <ide:EditorPanel id="editorPanel"
                     width="100%"
                     height="100%"
                     left="55"
                     right="255"
                     top="35"
                     bottom="23"/>
    
    <ide:ObjectsPanel id="objectsPanel"
                    width="250"
                    height="100%"
                    right="0"
                    top="35"
                    bottom="23"/>
    
    <ide:StatusBar id="statusbar"
                   width="100%"
                   height="23"
                   bottom="0"/>
</s:WindowedApplication>
